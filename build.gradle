apply plugin: 'java-library'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

configurations {
    jmh
    profiling
}

def getGitVersion(){
    "git describe --tags --always --first-parent".execute().text.trim()
}

def getGitCredentials(){
    def process = "git credential fill".execute()
    def stream = new PrintStream(process.getOutputStream(), true)
    stream.println("url=https://github.com/")
    stream.close()

    process.text.trim().split("\n").collectEntries {it.split("=")}
}

def getGitUser(){
    System.getenv("GITHUB_USER") ?: gitCredentials["username"]
}

def getGitPassword(){
    System.getenv("GITHUB_TOKEN") ?: gitCredentials["password"]
}

group = 'myworld'
version = getGitVersion()

dependencies {

    testImplementation(platform('org.junit:junit-bom:5.11.4'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')

    jmh 'org.openjdk.jmh:jmh-core:1.37'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }

    profiling {
        java {
            srcDir 'src/profiling/java'
            compileClasspath += main.compileClasspath + main.output + project.configurations.jmh + test.output
            runtimeClasspath += main.output
        }
        resources {
            srcDir 'src/test/resources'
            srcDir 'src/profiling/resources'
        }
    }

    jmh {
        java {
            srcDir 'src/jmh/java'
            compileClasspath += main.compileClasspath + main.output + project.configurations.jmh + test.output
            runtimeClasspath += main.output
            annotationProcessorPath = project.configurations.jmh

        }
        resources {
            srcDir 'src/jmh/resources'
        }
    }
}

task jmhCompile(type: JavaCompile, dependsOn: jmhClasses) {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(21)
    }
    source file('build/generated/sources/annotationProcessor/java/jmh/')
    classpath = sourceSets.jmh.runtimeClasspath +
            sourceSets.jmh.compileClasspath +
            sourceSets.jmh.output

    destinationDirectory.set(file('build/classes/java/jmh'))
}

task jmh(type: JavaExec, dependsOn: jmhCompile) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }

    mainClass = 'myworld.hummingbird.benchmark.Benchmarks'

    classpath = sourceSets.jmh.runtimeClasspath +
            sourceSets.jmh.compileClasspath +
            sourceSets.jmh.output
}

tasks.named("processJmhResources") {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.named("processProfilingResources") {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task runProfiler(type:JavaExec) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }

    mainClass = 'myworld.hummingbird.profiling.Profiler'

    classpath = sourceSets.profiling.runtimeClasspath + sourceSets.profiling.compileClasspath
}

publishing {
    publications {
        hummingbird(MavenPublication) {
            groupId = "${project.group}"
            artifactId = 'hummingbird'
            version = "${project.version}"

            from components.java
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MyWorldLLC/Packages")
            credentials {
                username = gitUser
                password = gitPassword
            }
        }
    }
}